(* ::Package:: *)

(*
Copyright: Giovanni Cerchiari, Lorenzo Dania, Yannick Weiser, Dmitry Bykov
e-mail: giovanni.cerchiari@uibk.ac.at
date : 08/2022
*)
(*This file is a free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This file is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with the other repository files.
    If not, it can be found at <https://www.gnu.org/licenses/>.
*)
(*
-----------------------------------------------------------------------
This file contains the script-program that has been used to evaluate
the results presented in the article:

If you wish to cite this work, we prepared a citation file "selective_suppression.bib"
in bibtex format in the repository.

This file was modified from 
doi: 10.5281/zenodo.4545692
*)
(*
-----------------------------------------------------------------------
The script is written for Mathematica 11.3 .
It was executed sucessfully on a machine with the following specifications
- operating system : Windows 10
- processor : Intel(R) Core(TM) i5-7300U CPU @ 2.60GHz 2.71 GHz
- RAM : 16 GB
-----------------------------------------------------------------------
*)
(*---------------------------------------------------------------------------------------------*)
(*---------------------------------------------------------------------------------------------*)
(*---------------------------------------------------------------------------------------------*)
ClearAll["Global`*"]
Needs["PlotLegends`"]
SetCoordinates[Spherical]
(*Generic direction in space*)
nr[\[Theta]_,\[Phi]_]:={Cos[\[Phi]]*Sin[\[Theta]],Sin[\[Phi]]*Sin[\[Theta]],Cos[\[Theta]]};
n\[Theta][\[Theta]_,\[Phi]_]:={Cos[\[Phi]]*Cos[\[Theta]],Sin[\[Phi]]*Cos[\[Theta]],-Sin[\[Theta]]};
n\[Phi][\[Theta]_,\[Phi]_]:={-Sin[\[Phi]],Cos[\[Phi]],0};
Print["Generic direction in space (\[Theta],\[Phi]) = ", MatrixForm[nr[\[Theta],\[Phi]]]]
(*---------------------------------------------------------------------------------------------*)
(*---------------------------------------------------------------------------------------------*)
(*RADIATED POWER PER UNIT SOLID ANGLE*)
(*differential scattered power into the solid andgle d\[CapitalOmega] = Sin[\[Theta]]d\[Theta]d\[Phi] *)
(*eq. 6 of the main text*)
(*Power radiated by a dipole:
- Pdip integral power of the dipole
- \[Theta]0,\[Phi]0 direction of linear polarization
- \[Theta],\[Phi] direction scattered light*)
(*Please remember the Sin[\[Theta]] missing factor at integration*)
(*linear polarization general case*)
dpdipd\[CapitalOmega][\[Theta]_,\[Phi]_,\[Theta]0_,\[Phi]0_]:=3/(8*\[Pi])*Pdip*(1-(nr[\[Theta],\[Phi]].nr[\[Theta]0,\[Phi]0])^2);
Print["(dpdipd\[CapitalOmega]) differential power radiate by a dipole
 with linear polarization: ",dpdipd\[CapitalOmega][\[Theta],\[Phi],\[Theta]p,\[Phi]p]]


(*---------------------------------------------------------------------------------------------*)
(*---------------------------------------------------------------------------------------------*)
(*SCATTERED POWER BY A LEVITATED SPHERE*)
(*The sphere has radius R0 and it is located in the center of the reference system*)
(*---------------------------------------------------------------------------------------------*)
(*Optical Green's function in the measurement region (free space)*)
gm[\[Theta]k_,\[Phi]k_,\[Theta]0_,\[Phi]0_,\[Theta]_,\[Phi]_,r0_]:=Exp[I*(2*\[Pi]*r0/\[Lambda])*nr[\[Theta]k,\[Phi]k].nr[\[Theta]0,\[Phi]0]]*Exp[I*(2*\[Pi]*r0/\[Lambda])*nr[\[Theta],\[Phi]].nr[\[Theta]0,\[Phi]0]];
(*Optical Green's function of the image generated by the hemipherical mirror. It cannot be used directly*)
gi[\[Theta]k_,\[Phi]k_,\[Theta]0_,\[Phi]0_,\[Theta]_,\[Phi]_,\[Rho]_,R_,r0_]:=-\[Rho]*Exp[I*(2*\[Pi]*r0/\[Lambda])*nr[\[Theta]k,\[Phi]k].nr[\[Theta]0,\[Phi]0]]*Exp[-I*(2*\[Pi]*r0/\[Lambda])*(nr[\[Theta],\[Phi]].nr[\[Theta]0,\[Phi]0]-2*R/r0)];
(*Optical Green's function in the control region: direct field + image field*)
gc[\[Theta]k_,\[Phi]k_,\[Theta]0_,\[Phi]0_,\[Theta]_,\[Phi]_,r0_]:=Evaluate[Simplify[gm[\[Theta]k,\[Phi]k,\[Theta]0,\[Phi]0,\[Theta],\[Phi],r0]+gi[\[Theta]k,\[Phi]k,\[Theta]0,\[Phi]0,\[Theta],\[Phi],1,\[Lambda],r0]]];
(*Simplify conditions: angles real, wavelenght positive, radius positive*)
simplfyGreenConditions={\[Lambda]>0, Element[\[Theta], Reals], Element[\[Phi], Reals], Element[\[Phi]0, Reals], Element[\[Theta]0, Reals], Element[\[Phi]k, Reals], Element[\[Theta]k, Reals], r0>0, R0>0};
(*Green's functions to n-th order*)
gm2o = Simplify[Normal[Series[gm[\[Pi]/2,\[Pi]/2,\[Theta]0,\[Phi]0,\[Theta],\[Phi],r0],{r0,0,2}]], Assumptions->simplfyGreenConditions];
(*gm4o = Simplify[Normal[Series[gm[\[Pi]/2,\[Pi]/2,\[Theta]0,\[Phi]0,\[Theta],\[Phi],r0],{r0,0,4}]], Assumptions\[Rule]simplfyGreenConditions];*)
gc2o = Simplify[Normal[Series[gc[\[Pi]/2,\[Pi]/2,\[Theta]0,\[Phi]0,\[Theta],\[Phi],r0],{r0,0,2}]], Assumptions->simplfyGreenConditions];
gc4o = Simplify[Normal[Series[gc[\[Pi]/2,\[Pi]/2,\[Theta]0,\[Phi]0,\[Theta],\[Phi],r0],{r0,0,4}]], Assumptions->simplfyGreenConditions];
(*The integral of the Green's function over the levitated sphere gives the electric field on the unitary sphere where the detector is located.*)
(*Normalization factors are disregarded because we are interested only in relative variations between the measurement region and the control region.*)
Em2det=Integrate[Integrate[Integrate[gm2o*Sin[\[Theta]0],{\[Theta]0,0,\[Pi]}],{\[Phi]0,0,2*\[Pi]}],{r0,0,R0}];
(*Em4det=Integrate[Integrate[Integrate[gm4o*Sin[\[Theta]0],{\[Theta]0,0,\[Pi]}],{\[Phi]0,0,2*\[Pi]}],{r0,0,R0}];*)
Ec2det=Integrate[Integrate[Integrate[gc2o*Sin[\[Theta]0],{\[Theta]0,0,\[Pi]}],{\[Phi]0,0,2*\[Pi]}],{r0,0,R0}];
Ec4det=Integrate[Integrate[Integrate[gc4o*Sin[\[Theta]0],{\[Theta]0,0,\[Pi]}],{\[Phi]0,0,2*\[Pi]}],{r0,0,R0}];
(*The power is proportional to the square of the electric field*)
pm2det = Simplify[Conjugate[Em2det]*Em2det, Assumptions->simplfyGreenConditions];
(*pm4det = Simplify[Conjugate[Em4det]*Em4det, Assumptions\[Rule]simplfyGreenConditions];*)
pc2det = Simplify[Conjugate[Ec2det]*Ec2det, Assumptions->simplfyGreenConditions];
pc4det = Simplify[Conjugate[Ec4det]*Ec4det, Assumptions->simplfyGreenConditions];
(*Integrals of the power in the measurement region and in the control region. Warning: different domains of integration.*)
Pm2det\[Theta] = Integrate[pm2det*Sin[\[Theta]],{\[Phi],0,2*\[Pi]}];
Pm2det[\[Theta]D_,RR0_]:=Evaluate[Simplify[Integrate[Pm2det\[Theta],{\[Theta],0,\[Theta]D}]+Integrate[Pm2det\[Theta],{\[Theta],\[Pi]-\[Theta]D,\[Pi]}], Assumptions->simplfyGreenConditions]/.{R0->RR0}];
Pm2detout[\[Theta]D_,RR0_]:=Evaluate[Simplify[Integrate[Pm2det\[Theta],{\[Theta],\[Theta]D,\[Pi]-\[Theta]D}], Assumptions->simplfyGreenConditions]/.{R0->RR0}];
(*Pm4det\[Theta] = Integrate[pm4det*Sin[\[Theta]],{\[Phi],0,2*\[Pi]}];*)
(*Pm4det[\[Theta]D_,RR0_]:=Evaluate[Simplify[Integrate[Pm4det\[Theta],{\[Theta],0,\[Theta]D}]+Integrate[Pm4det\[Theta],{\[Theta],\[Pi]-\[Theta]D,\[Pi]}], Assumptions\[Rule]simplfyGreenConditions]/.{R0\[Rule]RR0}];*)
Pc2det[\[Theta]D_,RR0_]:=Evaluate[Integrate[Integrate[pc2det*Sin[\[Theta]],{\[Phi],0,2*\[Pi]}],{\[Theta],\[Theta]D,\[Pi]/2}]/.{R0->RR0}];
Pc4det[\[Theta]D_,RR0_]:=Evaluate[Integrate[Integrate[pc4det*Sin[\[Theta]],{\[Phi],0,2*\[Pi]}],{\[Theta],\[Theta]D,\[Pi]/2}]/.{R0->RR0}];
(*Estimate of how good is the series approximation as a function of the radius R0 the levitated sphere. This is done by comparing the calcualtion
in series expansion to order 2 and order 4.*)
relativeuncertainty[R0_]:=Evaluate[Simplify[(Pc2det[\[Theta]D,R0]-Pc4det[\[Theta]D,R0])/Pc2det[\[Theta]D,R0], Assumptions->{\[Theta]D>0,R0>0,\[Lambda]>0}]];
(*Power ratio between the scattered light in the measurement region and in the control region in presence of the hemipsherical mirror*)
Pratio2[\[Theta]D_,R0_]:=Evaluate[Simplify[Pc2det[\[Theta]D,R0]/Pm2det[\[Theta]D,R0]]];
Pratio4[\[Theta]D_,R0_]:=Evaluate[Simplify[Pc4det[\[Theta]D,R0]/Pm2det[\[Theta]D,R0]]];
(*Power ratio between the scattered light in the measurement region and in the control region without the hemipsherical mirror*)
Pratio2out[\[Theta]D_,R0_]:=Evaluate[Simplify[Pm2detout[\[Theta]D,R0]/Pm2det[\[Theta]D,R0]]];


(*------------------------------------------------------------------*)
(*Plots and values*)
Print["relative deviation of the scattered power between 2nd order to 4th order = ", relativeuncertainty[R0], "   (it does not depend on the angles!)"]
Print["relative deviation of the scattered power between 2nd order to 4th order at the condition R0=\[Lambda]/5 -> ",N[Simplify[relativeuncertainty[\[Lambda]/5]]]]
Plot[relativeuncertainty[R0]/.{\[Lambda]->1},{R0,0,0.5}]
LogPlot[{Pratio2[ArcSin[0.4],R0]/.{\[Lambda]->1},Pratio4[ArcSin[0.4],R0]/.{\[Lambda]->1}},{R0,0,0.3}]
LogPlot[{Pratio2[ArcSin[NA],\[Lambda]/10]/.{\[Lambda]->1},Pratio4[ArcSin[NA],\[Lambda]/10]/.{\[Lambda]->1}},{NA,0,1},PlotRange->{0.001,1}]
Print["scattered power ratio at NA=0.4, R0=\[Lambda]/10 (2nd order) = ", N[Simplify[Pratio2[ArcSin[0.4],\[Lambda]/10]]]]
Print["scattered power ratio at NA=0.4, R0=\[Lambda]/10 (4th order) = ", N[Simplify[Pratio4[ArcSin[0.4],\[Lambda]/10]]]]
Print["scattered power ratio at NA=0.4, R0=\[Lambda]/5  (2nd order) = ", N[Simplify[Pratio2[ArcSin[0.4],\[Lambda]/5]]]]
Print["scattered power ratio at NA=0.4, R0=\[Lambda]/5  (4th order) = ", N[Simplify[Pratio4[ArcSin[0.4],\[Lambda]/5]]]]
Print["scattered power ratio at NA=0.4, R0=\[Lambda]/10 (2nd order) = ", N[Simplify[Pratio2out[ArcSin[0.4],\[Lambda]/10]]]]
Print["scattered power ratio at NA=0.4, R0=\[Lambda]/5  (2nd order) = ", N[Simplify[Pratio2out[ArcSin[0.4],\[Lambda]/5]]]]


(*----------------------------------------------------------------------------------------------*)
(*----------------------------------------------------------------------------------------------*)
(*BACK ACTION CENTER OF MASS WITH SELECTIVE SUPPRESSION*)
(*power spectral density of the radiated power along the direction defined by \[Theta] and \[Phi]*)
dspp[\[Theta]_,\[Phi]_,\[Theta]p_,\[Phi]p_]:=hbar*(c/\[Lambda])*dpdipd\[CapitalOmega][\[Theta],\[Phi],\[Theta]p,\[Phi]p];
(*power spectral density fluctuation of the radiation pressure force*)
dsba[\[Theta]_,\[Phi]_,\[Theta]p_,\[Phi]p_,\[Theta]0_,\[Phi]0_]:=(nr[\[Theta],\[Phi]].nr[\[Theta]0,\[Phi]0])^2*dspp[\[Theta],\[Phi],\[Theta]p,\[Phi]p]/c^2;
(*Back action. Integral of the force fluctuations.*)
(*We select the light polarization aligned to x-axis (\[Theta]p = \[Pi]/2, \[Phi]p = 0)*)
Sba[\[Theta]_,\[Theta]0_,\[Phi]0_]:=Evaluate[Simplify[Integrate[Simplify[Integrate[dsba[\[Theta]\[Theta],\[Phi],\[Pi]/2,0,\[Theta]0,\[Phi]0]*Sin[\[Theta]\[Theta]],{\[Phi],0,2*\[Pi]}]], {\[Theta]\[Theta],0,\[Theta]}]]];
Sbavet[\[Theta]_]:=Evaluate[Simplify[{Sba[\[Theta],\[Pi]/2,0], Sba[\[Theta],\[Pi]/2,\[Pi]/2], Sba[\[Theta],0,0]}]];
(*Ratio between controlled back action and maximum back action in free space.*)
(*The factor of 2 in front account for both sides of the uncontrolled region.*)
Sbar[\[Theta]_,\[Theta]0_,\[Phi]0_]:=Evaluate[Simplify[2*Sba[\[Theta], \[Theta]0, \[Phi]0]/Sba[\[Pi], 0, 0]]];
Sbarvet[\[Theta]_]:=Evaluate[Simplify[{Sbar[\[Theta],\[Pi]/2,0], Sbar[\[Theta],\[Pi]/2,\[Pi]/2], Sbar[\[Theta],0,0]}]];
(*Print*)
Print["BackAction(x, free space) = ", Sbavet[\[Theta]][[1]]]
Print["BackAction(y, free space) = ", Sbavet[\[Theta]][[2]]]
Print["BackAction(z, free space) = ", Sbavet[\[Theta]][[3]]]
Print["BackAction(x,controlled/free) = ", Sbarvet[\[Theta]][[1]]]
Print["BackAction(y,controlled/free) = ", Sbarvet[\[Theta]][[2]]]
Print["BackAction(z,controlled/free) = ", Sbarvet[\[Theta]][[3]]]
Print["BackAction(x,controlled/free) = ", Series[Sbarvet[\[Theta]][[1]],{\[Theta],0,4}]]
Print["BackAction(y,controlled/free) = ", Series[Sbarvet[\[Theta]][[2]],{\[Theta],0,4}]]
Print["BackAction(z,controlled/free) = ", Series[Sbarvet[\[Theta]][[3]],{\[Theta],0,4}]]


(*----------------------------------------------------------------------------------------------*)
(*----------------------------------------------------------------------------------------------*)
(*MEASUREMENT IMPRECISION OF THE SELF-HOMODYNE METHOD. LINEAR POLARIZATION*)
(*----------------------------------------------------------------------------------------------*)
(*The label "mirror" in the names of the variables refers to the self-homodyne method*)
(*----------------------------------------------------------------------------------------------*)
(*The imprecision s and S are described in the article Eqs 10 and 11: 
s = differential power spectral density of the imprecision noise associated with a differential 
    detector under direction (\[Theta], \[Phi])
S = integral of s in the angular integral \[Phi]=[0, 2\[Pi]] and \[Theta]=[0,\[Theta]]. Note the free parameter \[Theta].
With SS and ss natural units are introduced*)
(*Assumptions: shot noise dominated by the constant term*)
(*-------------------*)
(*differential imprecision for the self-homodyne configuration*)
smirror[\[Theta]_,\[Phi]_,\[Theta]p_,\[Phi]p_,\[Theta]0_,\[Phi]0_]:= Evaluate[Simplify[
	(hbar*c/\[Lambda])*(1+R^2)/( (4*R*(2*\[Pi]/\[Lambda])*(nr[\[Theta]0,\[Phi]0].nr[\[Theta],\[Phi]]))^2 *dpdipd\[CapitalOmega][\[Theta],\[Phi],\[Theta]p,\[Phi]p]),
	Assumptions->{0<=\[Theta]<=\[Pi], 0<=\[Phi]<2*\[Pi]}]];
Print["smirror = ", smirror[\[Theta],\[Phi],\[Pi]/2, 0, \[Theta]0,\[Phi]0]]
(*Imprecision for the self-homodyne configuration*)
Smirror[\[Theta]_,\[Theta]0_,\[Phi]0_]:=Evaluate[Simplify[
	(Integrate[Sin[\[Theta]\[Theta]]/smirror[\[Theta]\[Theta],\[Phi],\[Pi]/2,0,\[Theta]0,\[Phi]0],{\[Theta]\[Theta],0,\[Theta]},{\[Phi],0,2*\[Pi]}])^-1,
	 Assumptions->{0<=\[Theta]<=\[Pi]/2}]];
Smirrorvet[\[Theta]_]:=Evaluate[Simplify[{Smirror[\[Theta],\[Pi]/2,0], Smirror[\[Theta],\[Pi]/2,\[Pi]/2], Smirror[\[Theta],0,0]},{Assumptions->{R==1}}]];
Print["Imprecision self-homodyne(x) = ", Smirrorvet[\[Theta]][[1]]]
Print["Imprecision self-homodyne(y) = ", Smirrorvet[\[Theta]][[2]]]
Print["Imprecision self-homodyne(z) = ", Smirrorvet[\[Theta]][[3]]]
(*Enforce reference system with light polarization on the x-axis (\[Theta]p = \[Pi]/2, \[Phi]p = 0) and introducing natural units.*)
SSmirror[\[Theta]_,\[Theta]0_,\[Phi]0_]:= Evaluate[Simplify[Smirror[\[Theta],\[Pi]/2,0,\[Theta]0,\[Phi]0],
	Assumptions->{0<=\[Theta]<=\[Pi]/2, 0<=\[Phi]<2*\[Pi], 0<=\[Theta]0<=\[Pi], 0<=\[Phi]0<2*\[Pi], hbar==1, \[Lambda]==1, c==1, R==1, Pdip==1}]];
ssmirror[\[Theta]_,\[Phi]_,\[Theta]0_,\[Phi]0_]:= Evaluate[FullSimplify[smirror[\[Theta],\[Phi],\[Pi]/2,0,\[Theta]0,\[Phi]0],
	Assumptions->{0<=\[Theta]<=\[Pi]/2, 0<=\[Phi]<2*\[Pi], 0<=\[Theta]0<=\[Pi], 0<=\[Phi]0<2*\[Pi], hbar==1, \[Lambda]==1, c==1, R==1, Pdip==1}]];


(*----------------------------------------------------------------------------------------------*)
(*----------------------------------------------------------------------------------------------*)
(*PRODUCT OF IMPRECISION AND BACK ACTION FOR HYBRID SYSTEM COMPOSED BY THE HOLLOW MIRROR AND SELF-HOMODYNE*)
Print["detection limit x = ", Simplify[Smirrorvet[\[Theta]][[1]]*Sbavet[\[Theta]][[1]]]]
Print["detection limit y = ", Simplify[Smirrorvet[\[Theta]][[2]]*Sbavet[\[Theta]][[2]]]]
Print["detection limit z = ", Simplify[Smirrorvet[\[Theta]][[3]]*Sbavet[\[Theta]][[3]]]]


(*----------------------------------------------------------------------------------------------*)
(*----------------------------------------------------------------------------------------------*)
(*IDEAL MEASUREMENT ROTATION*)



(*----------------------------------------------------------------------------------------------*)
(*----------------------------------------------------------------------------------------------*)
(*GENERAL FORMULAS FOR SELF-HOMDYNE DETECTION*)
Print[""]
(*Maximum and minimum imprecision*)
minS = FullSimplify[SSmirror[ArcSin[1],\[Theta]pmax,\[Theta]0max,\[Phi]0max]];
maxS = FullSimplify[SSmirror[ArcSin[1],\[Theta]pmax,\[Pi]/2,0]];
Print["Minimum imprecision (natural units): ", minS];
Print["Minimum imprecision (natural units): ", maxS];
Print[""]
Print["differential imprecision"]
Print["smirror = ", smirror[\[Theta],\[Phi],\[Pi]/2,0,\[Theta]0,\[Phi]0]]
Print[""]
Print["Total imprecision \[Theta]0=0"]
Print["Smirror(\[Theta]0=0)= ", FullSimplify[Smirror[\[Theta]D,\[Pi]/2,0,0,0]]]
Print[""]
Print["Total imprecision \[Theta]0=\[Pi]/2"]
Print["Smirror(\[Theta]0=\[Pi]/2)= ", FullSimplify[Smirror[\[Theta]D,\[Pi]/2,0,\[Pi]/2,\[Phi]0]]]
Print[""]
Print["Total imprecision \[Theta]D=\[Pi]/2"]
Print["Smirror(\[Theta]=\[Pi]/2)= ", FullSimplify[Smirror[\[Pi]/2,\[Pi]/2,0,\[Theta]0,\[Phi]0]]]
Print[""]
Print["SmirrorQPD =", SmirrorQPD[\[Theta],\[Theta]0,\[Phi]0]]


Needs["PlotLegends`"]
(*----------------------------------------------------------------------------------------------*)
(*----------------------------------------------------------------------------------------------*)
(*PLOTS*)
(*----------------------------------------------------------------------------------------------*)
(*standard font size for all plots*)
lgdfontsize = 16;
(*frame flag that can be used for all plots*)
frameflg = False;
(*----------------------------------------------------------------------------------------------*)
(*3D axes label*)
xyzlabl = {Style["x",Bold,Black,lgdfontsize],Style["y",Bold,Black,lgdfontsize],Style["z",Bold,Black,lgdfontsize]};
(*2D axes label*)
xylabl = {Style["NA",Bold,Black,lgdfontsize],Style["Sba",Bold,Black,lgdfontsize]};
(*plot legend spec*)
pltlgdxyz100={Style["suppressed x",Black,lgdfontsize],Style["suppressed y",Black,lgdfontsize],Style["suppressed z",Black,lgdfontsize], Style["free space y-z",Black,lgdfontsize], Style["free space x",Black,lgdfontsize]};
(*line style spec*)
xyzlinestyle = {Directive[Red, Thick, Dashing[None]], Directive[Green, Thick, Dashing[None]],Directive[Blue, Thick, Dashing[None]],
Directive[Black, Thick, Dashed], Directive[Black, Thick, Dashed],Directive[Blue, Thick, Dashed],Directive[Orange, Thick, Dashing[None]], Directive[Purple, Thick, Dashing[None]]};
(*plotting*)
Plot[{Sbarvet[ArcSin[x]][[1]],Sbarvet[ArcSin[x]][[2]],Sbarvet[ArcSin[x]][[3]], Sba[\[Pi]/2,0,0]/Sba[\[Pi]/2,0,0], Sba[\[Pi]/2,\[Pi]/2,0]/Sba[\[Pi]/2,0,0]},{x,0,1},
	PlotLegend->pltlgdxyz100,
	LegendPosition->{-0.4,-0.2},LegendShadow->None,LegendSize->1,LegendBorder->Black,
	ImageSize->Large, AxesLabel -> xylabl , Axes -> True, Frame->frameflg, PlotStyle->xyzlinestyle, BaseStyle->{FontSize->16},
	 PlotTheme->"Monochrome", AspectRatio -> 3/4, Background->White]


(* --------------------------------------------*)
(* ORIENTATION OF A DIPOLE
The calculation is based on the technique described in
----
F. Tebbenjohanns, A. Militaru, A. Norrman, F. van der Laan, L. Novotny and M. Frimmer, "Optimal orientation detection of an anisotropic dipolar scatterer",
Phys. Rev. A 105, 053504 (2022), doi:10.1103/PhysRevA.100.043821
----
*)
dipole[\[Alpha]0_,E0_,\[CapitalDelta]\[Delta]_,\[CapitalDelta]\[Epsilon]_]:= \[Alpha]0*E0*{1,\[CapitalDelta]\[Delta],\[CapitalDelta]\[Epsilon]};
ui[\[Theta]_,\[Phi]_,n_]:=Sqrt[3/(8*\[Pi])]*(n\[Theta][\[Theta],\[Phi]]*n\[Theta][\[Theta],\[Phi]][[n]]+n\[Phi][\[Theta],\[Phi]]*n\[Phi][\[Theta],\[Phi]][[n]]);
(*checking orthogonality condition*)
uiuj[i_,j_]:= Integrate[Integrate[ui[\[Theta],\[Phi],i].ui[\[Theta],\[Phi],j]*Sin[\[Theta]],{\[Theta],0,\[Pi]}],{\[Phi],0,2*\[Pi]}];
Print["check ortoghonality conditions"]
Print["ux.ux = ", uiuj[1,1], "   ux.uy = ", uiuj[1,2],  "   ux.uz = ", uiuj[1,3]]
Print["uy.uy = ", uiuj[2,2], "   uy.uz = ", uiuj[2,3]]
Print["uz.uz = ", uiuj[3,3]]
(*Green's tensor*)
Gt[\[Theta]_,\[Phi]_]:=Sqrt[8*\[Pi]/3]*{ui[\[Theta],\[Phi],1], ui[\[Theta],\[Phi],2],ui[\[Theta],\[Phi],3]};
Print["Green's tensor = ", MatrixForm[Gt[\[Theta],\[Phi]]]]
(*Scattered electric field*)
Esc[\[Alpha]0_,E0_,\[CapitalDelta]\[Delta]_,\[CapitalDelta]\[Epsilon]_,\[Theta]_,\[Phi]_]:=(\[Omega]0^2/(4*\[Pi]*\[Epsilon]0*c^2))*Exp[((I*2*\[Pi])/\[Lambda])*r]*Gt[\[Theta],\[Phi]].dipole[\[Alpha]0,E0,\[CapitalDelta]\[Delta],\[CapitalDelta]\[Epsilon]];
(*Scattered intensity*)
simplifyconditions = {c>0, \[Epsilon]0>0, \[Alpha]0 > 0, \[Omega]0 > 0, \[CapitalDelta]\[Delta] > 0, \[CapitalDelta]\[Epsilon] > 0, E0 > 0, \[Lambda]>0, r>0, Element[\[Theta], Reals], Element[\[Phi], Reals]};
inte\[Theta]\[Phi] = Simplify[(\[Epsilon]0*c/2)*Conjugate[Esc[\[Alpha]0,E0,\[CapitalDelta]\[Delta],\[CapitalDelta]\[Epsilon],\[Theta],\[Phi]]].Esc[\[Alpha]0,E0,\[CapitalDelta]\[Delta],\[CapitalDelta]\[Epsilon],\[Theta],\[Phi]]*Sin[\[Theta]], Assumptions->simplifyconditions];
inte\[Theta] = Integrate[inte\[Theta]\[Phi],{\[Phi],0,2*\[Pi]}, Assumptions->simplifyconditions];
Intensity[\[Theta]DD_]:= Evaluate[Integrate[inte\[Theta],{\[Theta],0,\[Theta]D}, Assumptions->simplifyconditions]/.{\[Theta]D->\[Theta]DD}];
(*Integrated scattered power. The integral is doubled to consider the regions 0<\[Theta]<\[Theta]D and \[Pi]-\[Theta]D<\[Theta]<\[Pi] simultanously. Allowed values of \[Theta]D are in the interval [0,\[Pi]/2]*)
P0[\[Theta]D_]:= Simplify[2*Intensity[\[Theta]D],Assumptions->simplifyconditions];
Print["Scattered power free space. P0[\[Pi]/2] = ", Simplify[P0[\[Pi]/2]]]
Print["Scattered power in cone with condition \[Theta]<\[Theta]D. I[\[Theta]D] = ", Simplify[P0[\[Theta]D]]]
Print["Scattered power free space with \[CapitalDelta]\[Delta]=\[CapitalDelta]\[Epsilon]=0. I[\[Pi]/2] = ", Simplify[P0[\[Pi]/2]/.{\[CapitalDelta]\[Delta]->0, \[CapitalDelta]\[Epsilon]->0}]]
Print["Scattered power in cone with \[Theta]<\[Theta]D with \[CapitalDelta]\[Delta]=\[CapitalDelta]\[Epsilon]=0. I[\[Theta]D] = ", Simplify[P0[\[Theta]D]/.{\[CapitalDelta]\[Delta]->0, \[CapitalDelta]\[Epsilon]->0}]]


Spp=hbar*\[Omega]0*Plo/(2*\[Pi])
